#include "OutputAU.h"
#include "CoreAudioHelpers.h"
#include "AudioStats.h"

#include <Accelerate/Accelerate.h>
#import <AVFoundation/AVFoundation.h>
#if TARGET_OS_OSX
#import <IOKit/audio/IOAudioTypes.h>
#endif

#include <stdexcept>

#define kMaxBufferSize 4096
#define kBufferSizeFactor 1 // buffer will be 5ms * this
#define BUFFER_DURATION_MS 50 // XXX allow users to configure this

OutputAU::OutputAU()
  : m_SpatialBuffer(2, kMaxBufferSize),
    m_bitrateSum(0),
    m_opusPackets(0),
    m_opusToPCMTime(0.0),
    m_PCMToOutputTime(0.0)
{
    AudioComponentDescription description;
    description.componentType = kAudioUnitType_Output;
#if TARGET_OS_IPHONE
    description.componentSubType = kAudioUnitSubType_RemoteIO;
#elif TARGET_OS_OSX
    description.componentSubType = kAudioUnitSubType_HALOutput;
#endif
    description.componentManufacturer = kAudioUnitManufacturer_Apple;
    description.componentFlags = 0;
    description.componentFlagsMask = 0;

    auto comp = AudioComponentFindNext(nil, &description);
    if (!comp) {
        return;
    }

    OSStatus status = AudioComponentInstanceNew(comp, &m_OutputAU);
    if (status != noErr) {
        CA_LogError(status, "Failed to create an instance of HALOutput or RemoteIO");
        throw std::runtime_error("Failed to create an instance of HALOutput or RemoteIO");
    }
}

OutputAU::~OutputAU()
{
    if (m_OutputAU) {
        AudioComponentInstanceDispose(m_OutputAU);
    }
}

// Warning: realtime callback function
OSStatus renderCallbackSpatial(void                       * __nullable inRefCon,
                               AudioUnitRenderActionFlags * __nullable ioActionFlags,
                               const AudioTimeStamp       * __nullable inTimeStamp,
                               uint32_t                       inBusNumber,
                               uint32_t                       inNumberFrames,
                               AudioBufferList            * __nullable ioData)
{
    auto me = static_cast<OutputAU *>(inRefCon);
    AudioBufferList *spatialBuffer = me->m_SpatialBuffer.get();

    // Set the byte size with the output audio buffer list.
    for (uint32_t i = 0; i < spatialBuffer->mNumberBuffers; i++) {
        spatialBuffer->mBuffers[i].mDataByteSize = inNumberFrames * sizeof(float);
    }

    // Process the input frames with the audio unit spatial mixer.
    me->m_SpatialAU.process(spatialBuffer, inTimeStamp, inNumberFrames);

    // Copy the temporary buffer to the output.
    for (uint32_t i = 0; i < spatialBuffer->mNumberBuffers; i++) {
        // Accelerate version of memcpy(ioData->mBuffers[i].mData, spatialBuffer->mBuffers[i].mData, inNumberFrames * sizeof(float));
        //memcpy(ioData->mBuffers[i].mData, spatialBuffer->mBuffers[i].mData, inNumberFrames * sizeof(float));
        vDSP_mmov((const float *)spatialBuffer->mBuffers[i].mData, (float *)ioData->mBuffers[i].mData, 1, inNumberFrames * sizeof(float), 1, 1);
    }

    return noErr;
}

// lightweight callback debug logging
typedef enum {
    STARVED,
    OK
} CallbackState;

typedef struct {
    CallbackState state;
    int okCounter;
    int starvedCounter;
    int sinceStateChange;
} CallbackHealth;

static CallbackHealth ch = { STARVED, 0, 0, 0 };

// Warning: realtime callback function
OSStatus renderCallbackDirect(void                       * __nullable inRefCon,
                              AudioUnitRenderActionFlags * __nullable ioActionFlags,
                              const AudioTimeStamp       * __nullable inTimeStamp,
                              uint32_t                       inBusNumber,
                              uint32_t                       inNumberFrames,
                              AudioBufferList            * __nullable ioData)
{
    auto me = static_cast<OutputAU *>(inRefCon);
    int bytesToCopy = ioData->mBuffers[0].mDataByteSize;
    float *targetBuffer = (float *)ioData->mBuffers[0].mData;

    // Pull audio from playthrough buffer
    uint32_t availableBytes;
    float *buffer = (float *)TPCircularBufferTail(&me->m_RingBuffer, &availableBytes);

    if ((int)availableBytes < bytesToCopy) {
        // write silence if not enough buffered data is available
        // faster version of memset(targetBuffer, 0, bytesToCopy);
        //memset(targetBuffer, 0, bytesToCopy);
        vDSP_vclr(targetBuffer, 1, bytesToCopy);
        *ioActionFlags |= kAudioUnitRenderAction_OutputIsSilence;

        ch.starvedCounter++;
        if (ch.state == OK) {
            // Log only once when switching states
            DEBUG_TRACE(@"direct callback starved after %d OK callbacks: wanted %d, avail %d\n",
                        ch.okCounter, bytesToCopy, availableBytes);
            ch.okCounter = 0;
            ch.state = STARVED;
        }
    }
    else {
        // faster version of memcpy(targetBuffer, buffer, MIN(bytesToCopy, (int)availableBytes));
        //memcpy(targetBuffer, buffer, MIN(bytesToCopy, (int)availableBytes));
        vDSP_mmov(buffer, targetBuffer, 1, MIN(bytesToCopy, (int)availableBytes), 1, 1);
        TPCircularBufferConsume(&me->m_RingBuffer, MIN(bytesToCopy, (int)availableBytes));

        ch.okCounter++;
        if (ch.state == STARVED) {
            // Log only once when switching states
            DEBUG_TRACE(@"direct callback OK after %d starved callbacks: consumed %d\n",
                        ch.starvedCounter, MIN(bytesToCopy, (int)availableBytes));
            ch.starvedCounter = 0;
            ch.state = OK;
        }
    }

    return noErr;
}

bool OutputAU::prepareForPlayback(const OPUS_MULTISTREAM_CONFIGURATION* opusConfig)
{
    OSStatus status = noErr;

    m_sampleRateOpus  = opusConfig->sampleRate;
    m_sampleRateHW    = getSampleRate();
    m_channelCount    = opusConfig->channelCount;
    m_samplesPerFrame = opusConfig->samplesPerFrame;

    m_AudioPacketDuration = (m_samplesPerFrame / (m_sampleRateOpus / 1000.0)) / 1000.0; // 5ms

    if (!initAudioUnit()) {
        DEBUG_TRACE(@"initAudioUnit failed");
        return false;
    }

    if (!initRingBuffer()) {
        DEBUG_TRACE(@"initRingBuffer failed");
        return false;
    }

    AVAudioSession *session = [AVAudioSession sharedInstance];
    int physicalOutputChannels = (int)[session maximumOutputNumberOfChannels];

    AUSpatialMixerOutputType outputType = getSpatialMixerOutputType();

    Log(LOG_I, @"OutputAU preparing, input: opus %d channels, %d spf, %.3f packet size -> output: %d channels, %.0f khz, %f IOBuffer, %@",
        m_channelCount, m_samplesPerFrame, m_AudioPacketDuration,
        physicalOutputChannels, m_sampleRateHW, m_IOBufferDuration, getSMOTString(outputType));

    m_isSpatial = false;
    if (m_channelCount > 2) {
        if (outputType != kSpatialMixerOutputType_ExternalSpeakers) {
            m_isSpatial = true;
        }
    }

    // indicate the format our callback will provide samples in
    AudioStreamBasicDescription streamDesc;
    memset(&streamDesc, 0, sizeof(AudioStreamBasicDescription));
    streamDesc.mSampleRate       = m_sampleRateOpus;
    streamDesc.mFormatID         = kAudioFormatLinearPCM;
    streamDesc.mFormatFlags      = kAudioFormatFlagIsFloat | kAudioFormatFlagsNativeEndian | kAudioFormatFlagIsPacked;
    streamDesc.mFramesPerPacket  = 1;
    streamDesc.mChannelsPerFrame = (uint32_t)m_channelCount;
    streamDesc.mBitsPerChannel   = 32;
    streamDesc.mBytesPerPacket   = 4 * m_channelCount;
    streamDesc.mBytesPerFrame    = streamDesc.mBytesPerPacket;

    // XXX uncomment to test passing 8 channels to iOS
    // Does not currently work (only passes 2 channels)
    m_isSpatial = false;

    if (m_isSpatial) {
        // when the spatial mixer is used, the callback becomes non-interleaved
        streamDesc.mFormatFlags    |= kAudioFormatFlagIsNonInterleaved;
        streamDesc.mBytesPerPacket = 4;
        streamDesc.mBytesPerFrame  = 4;

        if (!m_SpatialAU.setup(outputType, m_sampleRateOpus, m_sampleRateHW, m_channelCount)) {
            DEBUG_TRACE(@"m_SpatialAU.setup failed");
            return false;
        }

        setCallback(this, renderCallbackSpatial);

        status = AudioUnitSetProperty(m_OutputAU, kAudioUnitProperty_StreamFormat, kAudioUnitScope_Input, 0, &streamDesc, sizeof(streamDesc));
        if (status != noErr) {
            CA_LogError(status, "Failed to set output stream format");
            return false;
        }

        Log(LOG_I, @"OutputAU is using spatial audio output");
    }
    else {
        // direct CoreAudio, for stereo or when enough real channels are available (HDMI)
        setCallback(this, renderCallbackDirect);

        status = AudioUnitSetProperty(m_OutputAU, kAudioUnitProperty_StreamFormat, kAudioUnitScope_Input, 0, &streamDesc, sizeof(streamDesc));
        if (status != noErr) {
            CA_LogError(status, "Failed to set output stream format");
            return false;
        }

        // XXX testing 8 channels to iOS
        if (m_channelCount > 2) {
            if (@available(iOS 15.0, tvOS 15.0, *)) {
                // not sure what this does
                NSError *error = nil;
                [session setSupportsMultichannelContent:YES error:&error];
                if (error != nil) {
                    Log(LOG_W, @"Warning: failed to setSupportsMultichannelContent:YES: %@", m_channelCount, error.localizedDescription);
                    // probably ok to continue
                }
                else {
                    DEBUG_TRACE(@"OutputAU setSupportsMultichannelContent:YES");
                }
            }
        }

        NSError *error = nil;
        Log(LOG_I, @"Multichannel output is available, will use passthrough mode");
        [session setPreferredOutputNumberOfChannels:m_channelCount error:&error];
        if (error != nil) {
            Log(LOG_W, @"Warning: failed to set preferred output number of channels to %d: %@", m_channelCount, error.localizedDescription);
            // probably ok to continue
        }
        DEBUG_TRACE(@"OutputAU setPreferredOutputNumberOfChannels:%d", m_channelCount);

        // Define the direct output stream format to ensure correct multichannel mapping
        AudioChannelLayoutTag layout;
        switch (m_channelCount) {
            case 2:
                layout = kAudioChannelLayoutTag_Stereo;
                break;
            case 6:
                layout = kAudioChannelLayoutTag_WAVE_5_1_B; // L R C LFE Rls Rrs
                break;
            case 8:
                layout = kAudioChannelLayoutTag_WAVE_7_1; // L R C LFE Rls Rrs Ls Rs
                break;
            default:
                CA_LogError(-1, "Unsupported number of channels for direct audio mode: %d", m_channelCount);
                return false;
        }

        AVAudioChannelLayout* outLayout = [AVAudioChannelLayout layoutWithLayoutTag:layout];
        AVAudioFormat *format = [[AVAudioFormat alloc] initWithCommonFormat:AVAudioPCMFormatFloat32
                                                                 sampleRate:m_sampleRateOpus
                                                                interleaved:YES
                                                              channelLayout:outLayout];

        const AudioStreamBasicDescription* asbd = [format streamDescription];
        CA_PrintASBD("OutputAU AudioStreamBasicDescription:", asbd);

        const AudioChannelLayout* outLayout2 = [outLayout layout];
        OSStatus status = AudioUnitSetProperty(m_OutputAU, kAudioUnitProperty_AudioChannelLayout, kAudioUnitScope_Input, 0, outLayout2, sizeof(AudioChannelLayout));
        if (status != noErr) {
            CA_LogError(status, "Failed to set OutputAU AudioChannelLayout scope=%d, layout=%d", kAudioUnitScope_Input, outLayout2);
            return status;
        }
        Log(LOG_I, @"OutputAU passthrough channel layout set for %d channels", m_channelCount);

//        {
//            // XXX trying to set headphones to 8 channels
//            status = AudioUnitSetProperty(m_OutputAU, kAudioUnitProperty_AudioChannelLayout, kAudioUnitScope_Output, 0, outLayout2, sizeof(AudioChannelLayout));
//            if (status != noErr) {
//                CA_LogError(status, "Failed to set OutputAU AudioChannelLayout scope=%d, layout=%d", kAudioUnitScope_Output, outLayout2);
//            }
//            Log(LOG_I, @"OutputAU passthrough channel layout set for %d channels", m_channelCount);
//        }
    }

    return true;
}

bool OutputAU::initAudioUnit()
{
    // Initialize the audio unit interface to begin configuring it.
    OSStatus status = AudioUnitInitialize(m_OutputAU);
    if (status != noErr) {
        CA_LogError(status, "Failed to initialize the output audio unit");
        return false;
    }

    /* macOS:
     * disable OutputAU input IO
     * enable OutputAU output IO
     * get system default output AudioDeviceID  (todo: allow user to choose specific device from list)
     * set OutputAU to AudioDeviceID
     * get device's AudioStreamBasicDescription (format, bit depth, samplerate, etc)
     * get device name
     * get output buffer frame size
     * get output buffer min/max
     * set output buffer frame size
     */

    m_OutputSoftwareLatencyMin = 0.0;
    m_OutputSoftwareLatencyMax = 0.0;
    m_TotalSoftwareLatency     = 0.0025; // Opus has 2.5ms of initial delay

#if TARGET_OS_OSX
    constexpr AudioUnitElement outputElement{0};
    constexpr AudioUnitElement inputElement{1};

    {
        uint32_t enableIO = 0;
        status = AudioUnitSetProperty(m_OutputAU, kAudioOutputUnitProperty_EnableIO, kAudioUnitScope_Input, inputElement, &enableIO, sizeof(enableIO));
        if (status != noErr) {
            CA_LogError(status, "Failed to disable the input on AUHAL");
            return false;
        }

        enableIO = 1;
        status = AudioUnitSetProperty(m_OutputAU, kAudioOutputUnitProperty_EnableIO, kAudioUnitScope_Output, outputElement, &enableIO, sizeof(enableIO));
        if (status != noErr) {
            CA_LogError(status, "Failed to enable the output on AUHAL");
            return false;
        }
    }

    {
        uint32_t size = sizeof(AudioDeviceID);
        AudioObjectPropertyAddress addr{kAudioHardwarePropertyDefaultOutputDevice, kAudioObjectPropertyScopeGlobal, kAudioObjectPropertyElementMain};
        status = AudioObjectGetPropertyData(AudioObjectID(kAudioObjectSystemObject), &addr, outputElement, nil, &size, &m_OutputDeviceID);
        if (status != noErr) {
            CA_LogError(status, "Failed to get the default output device");
            return false;
        }
    }

    {
        CFStringRef name;
        uint32_t nameSize = sizeof(CFStringRef);
        AudioObjectPropertyAddress addr{kAudioObjectPropertyName, kAudioObjectPropertyScopeGlobal, kAudioObjectPropertyElementMain};
        status = AudioObjectGetPropertyData(m_OutputDeviceID, &addr, 0, nil, &nameSize, &name);
        if (status != noErr) {
            CA_LogError(status, "Failed to get name of output device");
            return false;
        }
        setOutputDeviceName(name);
        CFRelease(name);
        DEBUG_TRACE(@"OutputAU default output device ID: %d, name: %s", m_OutputDeviceID, m_OutputDeviceName);
    }

    {
        // Set the current device to the default output device.
        // This should be done only after I/O is enabled on the output audio unit.
        status = AudioUnitSetProperty(m_OutputAU, kAudioOutputUnitProperty_CurrentDevice, kAudioUnitScope_Global, outputElement, &m_OutputDeviceID, sizeof(AudioDeviceID));
        if (status != noErr) {
            CA_LogError(status, "Failed to set the default output device");
            return false;
        }
    }

    {
        uint32_t streamFormatSize = sizeof(AudioStreamBasicDescription);
        AudioObjectPropertyAddress addr{kAudioDevicePropertyStreamFormat, kAudioDevicePropertyScopeOutput, kAudioObjectPropertyElementMain};
        status = AudioObjectGetPropertyData(m_OutputDeviceID, &addr, 0, nil, &streamFormatSize, &m_OutputASBD);
        if (status != noErr) {
            CA_LogError(status, "Failed to get output device AudioStreamBasicDescription");
            return false;
        }
        CA_PrintASBD("OutputAU output format:", &m_OutputASBD);
    }

    // Buffer:
    // The goal here is to set the system buffer to our desired value, which is currently in m_AudioPacketDuration.
    // First we get the current value, and the range of allowed values, set our value, and then query to find the actual value.
    // We also query the hardware latency (e.g. Bluetooth delay for AirPods), but this is just for fun

    {
        uint32_t bufferFrameSize = 0;
        uint32_t size = sizeof(uint32_t);
        AudioObjectPropertyAddress addr{kAudioDevicePropertyBufferFrameSize, kAudioObjectPropertyScopeOutput, kAudioObjectPropertyElementMain};
        status = AudioObjectGetPropertyData(m_OutputDeviceID, &addr, 0, nil, &size, &bufferFrameSize);
        if (status != noErr) {
            CA_LogError(status, "Failed to get the output device buffer frame size");
            return false;
        }
        DEBUG_TRACE(@"OutputAU output current BufferFrameSize %d", bufferFrameSize);
    }

    {
        AudioValueRange avr;
        uint32_t size = sizeof(AudioValueRange);
        AudioObjectPropertyAddress addr{kAudioDevicePropertyBufferFrameSizeRange, kAudioObjectPropertyScopeOutput, kAudioObjectPropertyElementMain};
        status = AudioObjectGetPropertyData(m_OutputDeviceID, &addr, 0, nil, &size, &avr);
        if (status != noErr) {
            CA_LogError(status, "Failed to get the output device buffer frame size range");
            return false;
        }
        m_OutputSoftwareLatencyMin = avr.mMinimum / m_OutputASBD.mSampleRate;
        m_OutputSoftwareLatencyMax = avr.mMaximum / m_OutputASBD.mSampleRate;
        DEBUG_TRACE(@"OutputAU output BufferFrameSizeRange: %.0f - %.0f", avr.mMinimum, avr.mMaximum);
    }

    // The latency values we have access to are:
    // kAudioDevicePropertyBufferFrameSize    our requested buffer as close to Opus packet size as possible
    //   + kAudioDevicePropertySafetyOffset   an additional CoreAudio buffer
    //   + kAudioUnitProperty_Latency         processing latency of OutputAU (+ SpatialAU in spatial mode)
    //   = total software latency
    // kAudioDevicePropertyLatency = hardware latency

    {
        double desiredBufferFrameSize = m_AudioPacketDuration;
        desiredBufferFrameSize = qMax(qMin(desiredBufferFrameSize, m_OutputSoftwareLatencyMax), m_OutputSoftwareLatencyMin);
        uint32_t bufferFrameSize = (uint32_t)(desiredBufferFrameSize * m_OutputASBD.mSampleRate);
        AudioObjectPropertyAddress addrSet{kAudioDevicePropertyBufferFrameSize, kAudioObjectPropertyScopeInput, kAudioObjectPropertyElementMain};
        status = AudioObjectSetPropertyData(m_OutputDeviceID, &addrSet, 0, NULL, sizeof(uint32_t), &bufferFrameSize);
        if (status != noErr) {
            CA_LogError(status, "Failed to set the output device buffer frame size");
            return false;
        }
        DEBUG_TRACE(@"OutputAU output requested BufferFrameSize of %d (%0.3f ms)", bufferFrameSize, desiredBufferFrameSize * 1000.0);

        // see what we got
        uint32_t size = sizeof(uint32_t);
        AudioObjectPropertyAddress addrGet{kAudioDevicePropertyBufferFrameSize, kAudioObjectPropertyScopeOutput, kAudioObjectPropertyElementMain};
        status = AudioObjectGetPropertyData(m_OutputDeviceID, &addrGet, 0, nil, &size, &m_BufferFrameSize);
        if (status != noErr) {
            CA_LogError(status, "Failed to get the output device buffer frame size");
            return false;
        }
        double bufferFrameLatency = (double)m_BufferFrameSize / m_OutputASBD.mSampleRate;
        m_TotalSoftwareLatency += bufferFrameLatency;
        DEBUG_TRACE(@"OutputAU output now has actual BufferFrameSize of %d (%0.3f ms)", m_BufferFrameSize, bufferFrameLatency * 1000.0);
    }

    {
        uint32_t safetyOffsetLatency = 0;
        uint32_t size = sizeof(safetyOffsetLatency);
        AudioObjectPropertyAddress addrGet{kAudioDevicePropertySafetyOffset, kAudioDevicePropertyScopeOutput, kAudioObjectPropertyElementMain};
        status = AudioObjectGetPropertyData(m_OutputDeviceID, &addrGet, 0, nil, &size, &safetyOffsetLatency);
        if (status != noErr) {
            CA_LogError(status, "Failed to get safety offset latency");
            return false;
        }
        m_TotalSoftwareLatency += (double)safetyOffsetLatency / m_OutputASBD.mSampleRate;
        DEBUG_TRACE(@"OutputAU OutputAU safety latency: %0.2f ms", ((double)safetyOffsetLatency / m_OutputASBD.mSampleRate) * 1000.0);
    }

    {
        uint32_t latencyFrames;
        uint32_t size = sizeof(uint32_t);
        AudioObjectPropertyAddress addr{kAudioDevicePropertyLatency, kAudioObjectPropertyScopeOutput, kAudioObjectPropertyElementMain};
        status = AudioObjectGetPropertyData(m_OutputDeviceID, &addr, 0, nil, &size, &latencyFrames);
        if (status != noErr) {
            CA_LogError(status, "Failed to get the output device hardware latency");
            return false;
        }
        m_OutputHardwareLatency = (double)latencyFrames / m_OutputASBD.mSampleRate;
        DEBUG_TRACE(@"OutputAU output hardware latency: %d (%0.2f ms)", latencyFrames, m_OutputHardwareLatency * 1000.0);
    }
#else
    AVAudioSession *session = [AVAudioSession sharedInstance];

    // set iOS metadata
    {
        NSError *error = nil;
        [session setPreferredSampleRate:m_sampleRateOpus error:&error];
        if (error != nil) {
            CA_LogError(-1, "failed to set preferred samplerate to %.0f: %@", m_sampleRateOpus, error.localizedDescription);
            // maybe ok?
        }
        // actual samplerate is checked in refreshDeviceMetadata() after setActive
    }

    // iOS buffer size
    {
        double wantedBuffer = m_AudioPacketDuration * kBufferSizeFactor;

        NSError *error = nil;
        [session setPreferredIOBufferDuration:wantedBuffer error:&error];
        if (error != nil) {
            CA_LogError(-1, "failed to set preferred buffer duration to %f: %@", wantedBuffer, error.localizedDescription);
            return false;
        }
        DEBUG_TRACE(@"OutputAU setPreferredIOBufferDuration %f", wantedBuffer);
        // actual buffer is checked in refreshDeviceMetadata() after setActive
    }

#endif

    // The time, in seconds, that it takes an audio unit to move an audio sample from its input to its output.
    {
        double audioUnitLatency = 0.0;
        uint32_t size = sizeof(audioUnitLatency);
        status = AudioUnitGetProperty(m_OutputAU, kAudioUnitProperty_Latency, kAudioUnitScope_Global, 0, &audioUnitLatency, &size);
        if (status != noErr) {
            CA_LogError(status, "Failed to get OutputAU AudioUnit latency");
            return false;
        }
        m_TotalSoftwareLatency += audioUnitLatency;
        DEBUG_TRACE(@"OutputAU AudioUnit latency: %0.2f ms", audioUnitLatency * 1000.0);
    }

    return true;
}

bool OutputAU::initRingBuffer()
{
    // init ring buffer, entries = 16 when duration = 80ms and packet size 5ms
    int packetsToBuffer = BUFFER_DURATION_MS / (m_samplesPerFrame / (m_sampleRateOpus / 1000.0));
    int ringBufferSize = packetsToBuffer * m_channelCount * m_samplesPerFrame * sizeof(float);
    bool ok = TPCircularBufferInit(&m_RingBuffer, ringBufferSize);
    if (!ok) {
        CA_LogError(-1, "TPCircularBufferInit failed");
        return false;
    }

    // Spatial mixer code needs to be able to read from the ring buffer
    m_SpatialAU.setRingBufferPtr(&m_RingBuffer);

    // real length will be larger than requested due to memory page alignment
    m_BufferSize = m_RingBuffer.length;
    DEBUG_TRACE(@"OutputAU ringBuffer created for %d packets, size %d (adjusted %d)",
                packetsToBuffer, ringBufferSize, m_BufferSize);

    return true;
}

AUSpatialMixerOutputType OutputAU::getSpatialMixerOutputType()
{
#if TARGET_OS_OSX

    // Check if headphones are plugged in.
    UInt32 dataSource{};
    UInt32 size = sizeof(dataSource);

    AudioObjectPropertyAddress addTransType{kAudioDevicePropertyTransportType, kAudioObjectPropertyScopeOutput, kAudioObjectPropertyElementMain};
    OSStatus status = AudioObjectGetPropertyData(m_OutputDeviceID, &addTransType, 0, nullptr, &size, &dataSource);
    if (status != noErr) {
        CA_LogError(status, "Failed to get the transport type of output device");
        return kSpatialMixerOutputType_ExternalSpeakers;
    }

    CA_FourCC(dataSource, m_OutputTransportType);
    DEBUG_TRACE(@"OutputAU output transport type %s", m_OutputTransportType);

    if (dataSource == kAudioDeviceTransportTypeHDMI) {
        dataSource = kIOAudioOutputPortSubTypeExternalSpeaker;
    } else if (dataSource == kAudioDeviceTransportTypeBluetooth || dataSource == kAudioDeviceTransportTypeUSB) {
        dataSource = kIOAudioOutputPortSubTypeHeadphones;
    } else {
        AudioObjectPropertyAddress theAddress{kAudioDevicePropertyDataSource, kAudioDevicePropertyScopeOutput, kAudioObjectPropertyElementMain};

        status = AudioObjectGetPropertyData(m_OutputDeviceID, &theAddress, 0, nullptr, &size, &dataSource);
        if (status != noErr) {
            CA_LogError(status, "Couldn't determine default audio device type, defaulting to ExternalSpeakers");
            return kSpatialMixerOutputType_ExternalSpeakers;
        }
    }

    CA_FourCC(dataSource, m_OutputDataSource);
    DEBUG_TRACE(@"OutputAU output data source %s", m_OutputDataSource);

    switch (dataSource) {
        case kIOAudioOutputPortSubTypeInternalSpeaker:
            return kSpatialMixerOutputType_BuiltInSpeakers;
            break;

        case kIOAudioOutputPortSubTypeHeadphones:
            return kSpatialMixerOutputType_Headphones;
            break;

        case kIOAudioOutputPortSubTypeExternalSpeaker:
            return kSpatialMixerOutputType_ExternalSpeakers;
            break;

        default:
            return kSpatialMixerOutputType_Headphones;
            break;
    }

#else

    AVAudioSession *audioSession = [AVAudioSession sharedInstance];

    if ([audioSession.currentRoute.outputs count] != 1) {
        DEBUG_TRACE(@"OutputAU current route has multiple outputs, spatial audio disabled");
        return kSpatialMixerOutputType_ExternalSpeakers;
    }
    else {
        NSString* pType = audioSession.currentRoute.outputs.firstObject.portType;
        DEBUG_TRACE(@"OutputAU current route port type %@", pType);
        if (   [pType isEqualToString:AVAudioSessionPortHeadphones]
            || [pType isEqualToString:AVAudioSessionPortBluetoothA2DP]
            || [pType isEqualToString:AVAudioSessionPortBluetoothLE]
            || [pType isEqualToString:AVAudioSessionPortBluetoothHFP]
            || [pType isEqualToString:AVAudioSessionPortUSBAudio])
        {
            return kSpatialMixerOutputType_Headphones;
        }
        else if ([pType isEqualToString:AVAudioSessionPortBuiltInSpeaker]) {
            return kSpatialMixerOutputType_BuiltInSpeakers;
        }
        else {
            return kSpatialMixerOutputType_ExternalSpeakers;
        }
    }

#endif
}

static NSString * const SMOT[] = {
    [kSpatialMixerOutputType_Headphones] = @"Headphones",
    [kSpatialMixerOutputType_BuiltInSpeakers] = @"BuiltInSpeakers",
    [kSpatialMixerOutputType_ExternalSpeakers] = @"ExternalSpeakers"
};

NSString *OutputAU::getSMOTString(AUSpatialMixerOutputType type)
{
    if (type >= 1 && type <= 3) {
        return SMOT[type];
    }
    return @"Unknown";
}

void OutputAU::setCallback(void *context, AURenderCallback callback)
{
    AURenderCallbackStruct renderCallback{ callback, context };

    OSStatus status = AudioUnitSetProperty(m_OutputAU, kAudioUnitProperty_SetRenderCallback, kAudioUnitScope_Output, 0, &renderCallback, sizeof(renderCallback));
    if (status != noErr) {
        CA_LogError(status, "Failed to set output render callback");
    }
}

void *OutputAU::getAudioBuffer(int *size)
{
    // This provides a buffer for the Opus API to write into.
    //
    // We must always write a full frame of audio. If we don't,
    // the reader will get out of sync with the writer and our
    // channels will get all mixed up. To ensure this is always
    // the case, round our bytes free down to the next multiple
    // of our frame size.
    uint32_t bytesFree;
    void *ptr = TPCircularBufferHead(&m_RingBuffer, &bytesFree);
    int bytesPerFrame = m_channelCount * sizeof(float);
    *size = MIN(*size, (int)(bytesFree / bytesPerFrame) * bytesPerFrame);

    m_BufferFilledBytes = m_RingBuffer.length - bytesFree;

    return ptr;
}

bool OutputAU::submitAudio(int bytesWritten, int opusBytes, CFTimeInterval decodeStartTime)
{
    // Called after Opus has decoded bytesWritten bytes of PCM into our buffer

    if (m_needsReinit) {
        // If an audio device has changed, this flag will be set. Break out
        // so we can be recreated.
        return false;
    }

    // drop packet if we've fallen behind Moonlight's queue by at least 30 ms
    int pendingAudio = LiGetPendingAudioDuration();
    if (pendingAudio > 30) {
        DEBUG_TRACE(@"submitAudio skip-ahead, pending audio duration: %d ms", pendingAudio);
        return true;
    }

    // Advance the write pointer
    TPCircularBufferProduce(&m_RingBuffer, bytesWritten);

    // accumulate stats
    m_bitrateSum += opusBytes;
    m_opusPackets++;
    m_opusToPCMTime += (CACurrentMediaTime() - decodeStartTime);

    return true;
}

double OutputAU::getSampleRate()
{
#if TARGET_OS_OSX
    AudioStreamBasicDescription asbd = {};
    uint32_t streamFormatSize = sizeof(AudioStreamBasicDescription);
    AudioObjectPropertyAddress streamFormatAddress{kAudioDevicePropertyStreamFormat, kAudioDevicePropertyScopeOutput, kAudioObjectPropertyElementMain};

    OSStatus status = AudioObjectGetPropertyData(m_OutputDeviceID, &streamFormatAddress, 0, nil, &streamFormatSize, &asbd);
    if (status != noErr) {
        return -1;
    }
    return asbd.mSampleRate;
#else
    return [[AVAudioSession sharedInstance] sampleRate];
#endif
}

bool OutputAU::start()
{
    return AudioOutputUnitStart(m_OutputAU) == noErr;
}

void OutputAU::refreshDeviceProperties() {
    AVAudioSession *session = [AVAudioSession sharedInstance];

    m_IOBufferDuration      = [session IOBufferDuration];
    m_sampleRateHW          = [session sampleRate];
    m_OutputHardwareLatency = [session outputLatency];
    m_outputChannels        = (int)[session outputNumberOfChannels];
    m_outputType            = getSMOTString(getSpatialMixerOutputType());

    // not sure if we want these yet
    int inputChannels                            = (int)[session inputNumberOfChannels];
    int maximumOutputNumberOfChannels            = (int)[session maximumOutputNumberOfChannels];
    AVAudioSessionMode mode                      = [session mode];
    bool supportsMultichannelContent             = false;
    AVAudioSessionRenderingMode renderingMode    = AVAudioSessionRenderingModeMonoStereo;

    DEBUG_TRACE(@"refreshed device: %@, %dch, sampleRate %.2f, IOBufferDuration %f, latency %d (%0.2f ms)",
                m_outputType, m_outputChannels, m_sampleRateHW, m_IOBufferDuration,
                (int)(m_OutputHardwareLatency * m_sampleRateHW), m_OutputHardwareLatency * 1000.0);

    if (@available(iOS 17.2, tvOS 17.2, *)) {

        AVAudioSessionRouteDescription *currentRoute = [session currentRoute];
        AVAudioSessionPortDescription *outputPort    = currentRoute.outputs.firstObject;
        bool isSpatialAudioEnabled                   = outputPort.isSpatialAudioEnabled;
        supportsMultichannelContent                  = [session supportsMultichannelContent];

        DEBUG_TRACE(@"isSpatialAudioEnabled %d, supportsMultichannelContent %d",
                    isSpatialAudioEnabled, supportsMultichannelContent);
        DEBUG_TRACE(@"currentRoute: %@", currentRoute);
        DEBUG_TRACE(@"channels: %@", outputPort.channels);
//        DEBUG_TRACE(@"renderingMode: %@", session.renderingMode);
    }

//    DEBUG_TRACE(@"inputChannels %d, maximumOutputNumberOfChannels %d, mode %@, renderingMode %@, supportsMultichannelContent %@",
//                inputChannels, maximumOutputNumberOfChannels, mode, renderingMode, supportsMultichannelContent);
}

bool OutputAU::stop()
{
    return AudioOutputUnitStop(m_OutputAU) == noErr;
}

bool OutputAU::isSpatial()
{
    return m_isSpatial;
}

OSStatus OutputAU::setOutputType(AUSpatialMixerOutputType outputType)
{
    return m_SpatialAU.setOutputType(outputType);
}

void OutputAU::setNeedsReinit(bool value)
{
    m_needsReinit = value;
}

// update rate is based on how often we're called
NSString * OutputAU::getAudioStatsString()
{
    static EWMA bitrateAvg(0.2);
    static EWMA decodeTimeAvg(0.2);
    static CFTimeInterval lastTick = CACurrentMediaTime();
    CFTimeInterval now = CACurrentMediaTime();

    // add a new sample to the bitrate moving average
    bitrateAvg.addSample((double)(m_bitrateSum * 8) / 1000.0 / (now - lastTick));

    // track audio decode time as the sum of opus decode -> ring buffer and ring buffer -> processing -> output
    // it doesn't include RTP processing time but probably should
    decodeTimeAvg.addSample((m_opusToPCMTime / 1000.0) / m_opusPackets);

    // next call will use data starting right now
    lastTick = now;
    m_bitrateSum = 0;
    m_opusPackets = 0;

    uint32_t freeBytes = 0;
    TPCircularBufferTail(&m_RingBuffer, &freeBytes);
    uint32_t pcmBytes = m_RingBuffer.length - freeBytes;
    double pcmDuration = pcmBytes * 1.0 / (m_channelCount * m_sampleRateOpus * sizeof(float));

    NSString *out = [NSString stringWithFormat:@"Audio stream: %dch Opus @ %.0f kbps\nBuffer: %.0f%% full (%.2f ms)\nAudio decode: %.2f ms",
                     m_channelCount, bitrateAvg.getOutput(),
                     (double)(pcmBytes * 100.0 / m_RingBuffer.length), pcmDuration * 1000.0,
                     decodeTimeAvg.getOutput()];

    DEBUG_TRACE(@"buffer health: %.2f %% full, PCM bytes: %d (%.2f ms), free bytes: %d, bitrate: %.0f kbps, decode time: %.2f ms",
                (double)(pcmBytes * 100.0 / m_RingBuffer.length), pcmBytes, pcmDuration * 1000.0, freeBytes, bitrateAvg.getOutput(), decodeTimeAvg.getOutput());

    return out;
}
